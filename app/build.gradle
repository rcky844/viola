import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            def sdkDir = properties.getProperty('git.exec')
            commandLine 'cmd', '/c', sdkDir, 'describe', '--match=NeVeRmAtCh', '--always', '--dirty'
        } else {
            commandLine 'git', 'describe', '--match=NeVeRmAtCh', '--always', '--dirty'
        }
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    namespace "tipz.viola"
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 34

    defaultConfig {
        applicationId "tipz.viola"
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 9
        versionName "6.0.0005.00"

        buildConfigField "String", "VERSION_NAME_EXTRA", "\"\""
        buildConfigField "String", "VERSION_CODENAME", "\"Viola\""
        buildConfigField "String", "VERSION_BUILD_DATE", "\"" + new Date().format("E MMM dd HH:mm:ss z yyyy") + "\""
        buildConfigField "String", "VERSION_BUILD_YEAR", "\"" + new Date().format("yyyy") + "\""

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        resConfigs 'de-rDE', 'en-rGB', 'fr-rFR', 'tr', 'vi', 'zh-rCN', 'zh-rTW'

    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        beta {
            initWith(debug)
            applicationIdSuffix ".beta"
            multiDexEnabled true
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" Beta 1\""
        }

        debug {
            applicationIdSuffix ".debug"
            multiDexEnabled true
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        debug {
            res.srcDir "debug/res"
        }
    }
}

dependencies {
    def room_version = "2.6.0"

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0-alpha01'
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'androidx.webkit:webkit:1.9.0-rc01'
    implementation('androidx.preference:preference-ktx:1.2.1') {
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel'
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    annotationProcessor 'androidx.annotation:annotation:1.7.0'
    annotationProcessor "androidx.room:room-compiler:$room_version"
}
