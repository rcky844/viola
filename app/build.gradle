plugins {
    id 'com.android.application'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    namespace 'tipz.browservio'
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 35
    buildFeatures.buildConfig = true

    defaultConfig {
        applicationId "tipz.browservio"
        minSdkVersion 14
        targetSdkVersion 34
        versionCode 69420
        versionName "MASTER"

        buildConfigField "String", "VERSION_NAME_EXTRA", "\"\""
        buildConfigField "String", "VERSION_CODENAME", "\"Dragon\""
        buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_dragon_master\""
        buildConfigField "String", "VERSION_BUILD_DATE", "\"" + new Date().format("yyMMdd-HHmm") + "\""
        buildConfigField "String", "VERSION_BUILD_YEAR", "\"" + new Date().format("yyyy") + "\""

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        resConfigs "de-rDE", "en-rGB", "tr", "vi", "zh-rCN", "zh-rTW"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
            buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_${getGitHash()}\""
        }

        snapshot {
            initWith(buildTypes.release)
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
            buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_${getGitHash()}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.7.0-alpha02'
    implementation 'androidx.webkit:webkit:1.5.0-alpha01'
    implementation 'androidx.preference:preference:1.2.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation "androidx.room:room-runtime:2.4.2"
    annotationProcessor "androidx.room:room-compiler:2.4.2"
}
