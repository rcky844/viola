plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--match=NeVeRmAtCh', '--always', '--dirty'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    namespace "tipz.viola"
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "tipz.viola"
        compileSdk 34
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 45
        versionName "6.4"

        buildConfigField "String", "VERSION_NAME_EXTRA", "\"\""
        buildConfigField "String", "VERSION_NAME_HUMAN_EXTRA", "\"\""
        buildConfigField "String", "VERSION_CODENAME", "\"Jumpas Update 2\""
        buildConfigField "String", "VERSION_BUILD_DATE", "\"" + new Date().format("E MMM dd HH:mm:ss z yyyy") + "\""
        buildConfigField "String", "VERSION_BUILD_YEAR", "\"" + new Date().format("yyyy") + "\""

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        resConfigs 'de-rDE', 'en-rGB', 'fr-rFR', 'tr', 'vi', 'zh-rCN', 'zh-rTW'

    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            multiDexEnabled true
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lintOptions {
        abortOnError = false
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        debug {
            res.srcDir "debug/res"
        }
    }
}

dependencies {
    def room_version = "2.6.1"

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'androidx.webkit:webkit:1.11.0'
    implementation('androidx.preference:preference-ktx:1.2.1') {
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel'
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    annotationProcessor 'androidx.annotation:annotation:1.8.0'
    annotationProcessor "androidx.room:room-compiler:$room_version"
}
